# Maintainer: Sebastian Lau <lauseb644 _at_ gmail.com>
# Contributor: twa022 <twa022 at gmail dot com>
# Contributor: American_Jesus <american.jesus.pt AT gmail DOT com>

pkgname=nemo-dropbox
pkgver=3.4.0
pkgrel=2
pkgdesc="Dropbox for Linux - Nemo extension"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/nemo-extensions"
license=('custom:CC-BY-ND-3' 'GPL')
depends=('nemo>=3.2' 'dropbox')
install=${pkgname}.install
options=('!libtool' '!emptydirs')
source=("nemo-extensions-$pkgver.tar.gz::https://github.com/linuxmint/nemo-extensions/archive/$pkgver.tar.gz"
        'remove_python_deps.patch')
sha256sums=('68dadce98c17321c3c22e596452a1b7aa0842c5faa05688734430857e037b3f2'
            'cc8048837c8da692933468908cc8ae36239c6c842d43f8790428326f67161abb')

prepare() {
  cd "${srcdir}/nemo-extensions-${pkgver}"
  patch -uNp2 -r- -i ../remove_python_deps.patch
  NOCONFIGURE=1 ./autogen.sh 
}

build() {
  cd "${srcdir}/nemo-extensions-${pkgver}/${pkgname}"
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/nemo-extensions-${pkgver}/${pkgname}"
  make DESTDIR="${pkgdir}" install
  # install the common license
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
